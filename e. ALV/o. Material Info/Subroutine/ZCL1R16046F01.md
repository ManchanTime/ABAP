``` abap
*&---------------------------------------------------------------------*
*& Include          ZCL1R16046F01
*&---------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*& Form set_init_value
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_init_value .

  pa_werks = '1010'.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form get_base_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM get_base_data .

  CLEAR gt_body.
  SELECT matnr werks ekgrp dispo beskz
         erdat ernam erzet aedat aenam aezet
    INTO CORRESPONDING FIELDS OF TABLE gt_body
    FROM zcl1mara16
    WHERE werks = pa_werks
      AND matnr IN so_matnr
      AND ekgrp IN so_ekgrp.

  IF gt_body IS INITIAL.
    MESSAGE s009 DISPLAY LIKE 'E'.
*    STOP.
  ELSE.
    MESSAGE s008 WITH sy-dbcnt.
  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form display_screen_0100
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM display_screen_0100 .

  IF go_container IS NOT BOUND.

    CLEAR : gt_fcat, gs_fcat.
    PERFORM set_fcat USING : 'X' 'MATNR' 'ZCL1MARA16' 'C' ' ',
                             'X' 'WERKS' 'ZCL1MARA16' 'C' ' ',
                             ' ' 'EKGRP' 'ZCL1MARA16' 'C' ' ',
                             ' ' 'DISPO' 'ZCL1MARA16' 'C' 'X',
                             ' ' 'BESKZ' 'ZCL1MARA16' 'C' ' '.
    PERFORM set_layout.
    PERFORM exclude_toolbar.
    PERFORM create_object.

    SET HANDLER : lcl_event_handler=>modify_value FOR go_alv_grid,
                  lcl_event_handler=>edit_toolbar FOR go_alv_grid,
                  lcl_event_handler=>user_command FOR go_alv_grid.

    CALL METHOD go_alv_grid->set_table_for_first_display
      EXPORTING
        is_variant           = gs_variant
        i_save               = 'A'
        i_default            = 'X'
        is_layout            = gs_layout
        it_toolbar_excluding = gt_ui_functions
      CHANGING
        it_outtab            = gt_body
        it_fieldcatalog      = gt_fcat.

    PERFORM set_register.
  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form SET_FCAT
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> P_
*&      --> P_
*&      --> P_
*&      --> P_
*&      --> P_
*&---------------------------------------------------------------------*
FORM set_fcat USING pv_key pv_field pv_table pv_just pv_emph.

  gs_fcat-key       = pv_key.
  gs_fcat-fieldname = pv_field.
  gs_fcat-ref_table = pv_table.
  gs_fcat-just      = pv_just.
  gs_fcat-emphasize = pv_emph.
  APPEND gs_fcat TO gt_fcat.
  CLEAR gs_fcat.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_layout
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_layout .

  gs_layout-zebra      = abap_true.
  gs_layout-cwidth_opt = 'A'.
  gs_layout-sel_mode   = 'D'.
  gs_layout-stylefname = 'CELL_TAB'.
*  gs_layout-no_toolbar = abap_true.

  gs_variant-report = sy-repid.
  gs_variant-handle = 'ALV1'.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form create_object
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM create_object .

  CREATE OBJECT go_container
    EXPORTING
      side      = go_container->dock_at_left
      extension = 5000.

  CREATE OBJECT go_alv_grid
    EXPORTING
      i_parent = go_container.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_modify_value
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> E_MODIFIED
*&      --> ET_GOOD_CELLS
*&---------------------------------------------------------------------*
FORM handle_modify_value USING pv_modified
                               pt_good_cells TYPE lvc_t_modi.

  DATA : ls_good_cells TYPE lvc_s_modi.

  CHECK pv_modified IS NOT INITIAL.

  LOOP AT pt_good_cells INTO ls_good_cells.

    CLEAR gs_body.
    gs_body-modi_yn = abap_true.

    MODIFY gt_body FROM gs_body INDEX ls_good_cells-row_id
                                TRANSPORTING modi_yn.

  ENDLOOP.

*  PERFORM refresh_table.
ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_user_command
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> E_UCOMM
*&---------------------------------------------------------------------*
FORM handle_user_command USING pv_ucomm.

  CASE pv_ucomm.
    WHEN 'SAVE'.
      PERFORM save_data.
    WHEN 'REFR'.
      PERFORM refresh_table.
      MESSAGE s000 WITH 'Refresh Ok'.
    WHEN 'TOGL'.
      PERFORM toggle_change.
    WHEN 'IROW'.
      PERFORM insert_row.
    WHEN 'DROW'.
      PERFORM delete_row.
  ENDCASE.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_edit_toolbar
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> E_OBJECT
*&      --> E_INTERACTIVE
*&---------------------------------------------------------------------*
FORM handle_edit_toolbar USING po_object TYPE
                                         REF TO cl_alv_event_toolbar_set
                               pv_interactive.

  DATA : lv_disabled.

  IF gv_mode = 0.
    lv_disabled = abap_true.
  ENDIF.

  PERFORM make_divide_line USING po_object.

  PERFORM make_btn USING 'REFR' icon_refresh
                         'Refresh screen' ' ' po_object.

  PERFORM make_btn USING 'SAVE' icon_system_save
                         'Save data' ' ' po_object.

  PERFORM make_divide_line USING po_object.

  PERFORM make_btn USING 'TOGL' icon_toggle_display_change
                         'Display <-> Change' ' ' po_object.

  PERFORM make_divide_line USING po_object.

  PERFORM make_btn USING 'IROW' icon_insert_row
                         'Insert row' lv_disabled po_object.
  PERFORM make_btn USING 'DROW' icon_delete_row
                         'Delete row' lv_disabled po_object.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form make_divide_line
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> PO_OBJCET
*&---------------------------------------------------------------------*
FORM make_divide_line USING po_object TYPE
                                      REF TO cl_alv_event_toolbar_set.

  CLEAR gs_button.
  gs_button-butn_type = '3'.
  APPEND gs_button TO po_object->mt_toolbar.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form make_btn
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> P_
*&      --> ICON_TOGGLE_DISPLAY_CHANGE
*&      --> P_
*&      --> LV_DISABLED
*&---------------------------------------------------------------------*
FORM make_btn USING pv_function pv_icon pv_quick pv_disabled
                    po_object TYPE REF TO cl_alv_event_toolbar_set.

  CLEAR gs_button.
  gs_button-function  = pv_function.
  gs_button-icon      = pv_icon.
  gs_button-quickinfo = pv_quick.
  gs_button-disabled  = pv_disabled.
  APPEND gs_button TO po_object->mt_toolbar.
ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_register
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_register .

  go_alv_grid->register_edit_event( go_alv_grid->mc_evt_modified ).
  go_alv_grid->register_edit_event( go_alv_grid->mc_evt_enter ).
*  go_alv_grid->set_ready_for_input( 1 ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form exclude_toolbar
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM exclude_toolbar .
  DATA : ls_ui_functions TYPE ui_func.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_undo.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_copy.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_copy_row.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_cut.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_delete_row.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_insert_row.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_append_row.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_paste.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_paste_new_row.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_refresh.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_auf.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_average.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_print.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_graph.
  APPEND ls_ui_functions TO gt_ui_functions.
ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_init_condition
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_init_condition .

  LOOP AT SCREEN.

    IF screen-name = 'PA_WERKS'.
      screen-input = 0.
    ENDIF.

    MODIFY SCREEN.
  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form insert_row
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM insert_row .

  DATA : ls_styl TYPE lvc_s_styl.

  CLEAR gs_body.
  gs_body-werks = pa_werks.

  ls_styl-style = cl_gui_alv_grid=>mc_style_enabled.
  INSERT ls_styl INTO TABLE gs_body-cell_tab.
  CLEAR ls_styl.

  PERFORM set_disabled USING 'WERKS'
                       CHANGING ls_styl.

  APPEND gs_body TO gt_body.

  PERFORM refresh_table.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form refresh_table
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM refresh_table .

  DATA : ls_stable TYPE lvc_s_stbl.

  ls_stable-row = abap_true.
  ls_stable-col = abap_true.

  CALL METHOD go_alv_grid->refresh_table_display
    EXPORTING
      is_stable = ls_stable.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form delete_row
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM delete_row .

  DATA : lt_row_id TYPE lvc_t_roid,
         ls_row_id TYPE lvc_s_roid,
         ls_body_d TYPE zcl1mara16.

  CALL METHOD go_alv_grid->get_selected_rows
    IMPORTING
      et_row_no = lt_row_id.

  IF lt_row_id IS INITIAL.
    MESSAGE s010 DISPLAY LIKE 'E'.
    EXIT.
  ENDIF.

  CLEAR : gt_body_d.
  SORT lt_row_id BY row_id DESCENDING.
  LOOP AT lt_row_id INTO ls_row_id.

    CLEAR : gs_body, ls_body_d.
    READ TABLE gt_body INTO gs_body INDEX ls_row_id-row_id.
    MOVE-CORRESPONDING gs_body TO ls_body_d.
    APPEND ls_body_d TO gt_body_d.
    DELETE gt_body INDEX ls_row_id-row_id.

  ENDLOOP.

  PERFORM refresh_table.
ENDFORM.
*&---------------------------------------------------------------------*
*& Form toggle_type
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
FORM toggle_change.

  gv_mode = ( gv_mode + 1 ) MOD 2.
  go_alv_grid->set_ready_for_input( gv_mode ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_screen
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_screen .

  DATA : ls_styl  TYPE lvc_s_styl,
         lv_tabix TYPE sy-tabix.

  CLEAR gs_body.
  LOOP AT gt_body INTO gs_body.

    lv_tabix = sy-tabix.

    CLEAR : ls_styl, gs_body-cell_tab.
    PERFORM set_enabled USING ' '
                        CHANGING ls_styl.
    PERFORM set_disabled USING 'MATNR'
                         CHANGING ls_styl.
    PERFORM set_disabled USING 'WERKS'
                         CHANGING ls_styl.
    MODIFY gt_body FROM gs_body INDEX lv_tabix TRANSPORTING cell_tab.
  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_enabled
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> P_
*&---------------------------------------------------------------------*
FORM set_enabled USING pv_field
                 CHANGING ps_styl TYPE lvc_s_styl.
  ps_styl-fieldname = pv_field.
  ps_styl-style     = cl_gui_alv_grid=>mc_style_enabled.
  APPEND ps_styl TO gs_body-cell_tab.
  CLEAR ps_styl.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form save_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM save_data .

  DATA : lv_answer,
         lt_save   TYPE TABLE OF zcl1mara16,
         ls_save   TYPE zcl1mara16,
         lv_tabix  TYPE sy-tabix,
         lv_check  TYPE abap_bool.

  DATA : lt_changed TYPE lvc_s_modi.

  CALL METHOD go_alv_grid->check_changed_data.

  CLEAR gs_body.
  READ TABLE gt_body INTO gs_body WITH KEY modi_yn = abap_true.
  IF ( sy-subrc <> 0 ) AND
     ( gt_body_d IS INITIAL ).
    MESSAGE s000 WITH TEXT-i01 DISPLAY LIKE 'E'.
    EXIT.
  ENDIF.

  PERFORM get_popup_to_confirm USING 'Save Ok?'
                               CHANGING lv_answer.

  CHECK lv_answer = '1'.

  PERFORM set_date.

  MOVE-CORRESPONDING gt_body TO lt_save.

*-- 데이터 삭제
  IF gt_body_d IS NOT INITIAL.
    DELETE zcl1mara16 FROM TABLE gt_body_d.
    CLEAR gt_body_d.
  ENDIF.


*-- 수정 및 생성 데이터 반영
  MODIFY zcl1mara16 FROM TABLE lt_save.

  IF sy-subrc = 0.
    MESSAGE s004.
    COMMIT WORK.
    PERFORM set_screen.
    PERFORM refresh_table.
  ELSE.
    MESSAGE s000 WITH 'Error' DISPLAY LIKE 'E'.
    ROLLBACK WORK.
  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form get_popup_to_confirm
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      <-- LV_ANSWER
*&---------------------------------------------------------------------*
FORM get_popup_to_confirm USING pv_title
                          CHANGING pv_answer.

  CALL FUNCTION 'POPUP_TO_CONFIRM'
    EXPORTING
      titlebar              = 'Save Check'
      text_question         = pv_title
      text_button_1         = 'Yes'
      icon_button_1         = 'ICON_OKAY'
      text_button_2         = 'No'
      icon_button_2         = 'ICON_CANCEL'
      default_button        = '1'
      display_cancel_button = 'X'
    IMPORTING
      answer                = pv_answer.
ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_date
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      <-- LT_SAVE
*&---------------------------------------------------------------------*
FORM set_date.

  DATA : lv_tabix TYPE sy-tabix.

  LOOP AT gt_body INTO gs_body WHERE modi_yn = abap_true.

    lv_tabix = sy-tabix.

    IF gs_body-erdat IS INITIAL.
      gs_body-erdat = sy-datum.
      gs_body-ernam = sy-uname.
      gs_body-erzet = sy-uzeit.
    ELSE.
      gs_body-aedat = sy-datum.
      gs_body-aenam = sy-uname.
      gs_body-aezet = sy-uzeit.

    ENDIF.

    CLEAR gs_body-modi_yn.

    MODIFY gt_body FROM gs_body INDEX lv_tabix TRANSPORTING
                                                erdat ernam erzet
                                                aedat aenam aezet
                                                modi_yn.
  ENDLOOP.
ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_disabled
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> P_
*&      <-- LS_STYL
*&---------------------------------------------------------------------*
FORM set_disabled USING pv_field
                  CHANGING ps_styl TYPE lvc_s_styl.

  ps_styl-fieldname = pv_field.
  ps_styl-style     = cl_gui_alv_grid=>mc_style_disabled.
  INSERT ps_styl INTO TABLE gs_body-cell_tab.
  CLEAR ps_styl.
ENDFORM.
