``` abap
*&---------------------------------------------------------------------*
*& Include          ZRC1_16_01F01
*&---------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*& Form get_base_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM get_base_data .

  CLEAR : gt_body.

*-- Get Main Data
*-- TimeStamp를 위해 TimeStamp 정보도 가져옴
  SELECT ryear rbukrs belnr racct remark hsl rtcur post_yn
         erdat ernam erzet aedat aenam aezet
    INTO CORRESPONDING FIELDS OF TABLE gt_body
    FROM ztc1_docu_16.

*-- Messaging db count
  MESSAGE s003 WITH sy-dbcnt.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form display_screen
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM display_screen .

  IF go_container IS NOT BOUND.

*-- field catalog 세팅
    CLEAR : gt_fcat, gs_fcat.
    PERFORM set_fcat USING : 'X' 'RYEAR'   'ZTC1_DOCU_16' 'C' ' ',
                             'X' 'RBUKRS'  'ZTC1_DOCU_16' 'C' ' ',
                             'X' 'BELNR'   'ZTC1_DOCU_16' 'C' ' ',
                             ' ' 'RACCT'   'ZTC1_DOCU_16' ' ' ' ',
*                             ' ' 'TXT50'   'SKAT'         ' ' 'X',
                             ' ' 'REMARK'  'ZTC1_DOCU_16' ' ' ' ',
                             ' ' 'HSL'     'ZTC1_DOCU_16' ' ' ' ',
                             ' ' 'RTCUR'   'ZTC1_DOCU_16' 'C' 'X',
                             ' ' 'POST_YN' 'ZTC1_DOCU_16' 'C' ' '.

*-- layout 세팅
    PERFORM set_layout.
*-- 필요없는 툴바 삭제
    PERFORM set_exclude_toolbar.
*-- 컨테이너 그리드 생성
    PERFORM create_object.

*-- 각종 이벤트 등록
    SET HANDLER : lcl_event_handler=>modify_value FOR go_alv_grid,
                  lcl_event_handler=>edit_toolbar FOR go_alv_grid,
                  lcl_event_handler=>user_command FOR go_alv_grid.
*-- ALV 출력
    CALL METHOD go_alv_grid->set_table_for_first_display
      EXPORTING
        is_variant           = gs_variant
        i_save               = 'A'
        i_default            = 'X'
        is_layout            = gs_layout
        it_toolbar_excluding = gt_ui_functions
      CHANGING
        it_outtab            = gt_body
        it_fieldcatalog      = gt_fcat.

*-- 변경 이벤트 등록
    PERFORM register_event.

  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_fcat
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> P_
*&      --> P_
*&      --> P_
*&      --> P_
*&      --> P_
*&---------------------------------------------------------------------*
FORM set_fcat USING pv_key pv_field pv_table pv_just pv_emph.

*-- 필드 카탈로그 기본 정보 세팅
  gs_fcat-key       = pv_key.
  gs_fcat-fieldname = pv_field.
  gs_fcat-ref_table = pv_table.
  gs_fcat-just      = pv_just.
  gs_fcat-emphasize = pv_emph.

  CASE pv_field.  " 필드 이름이 HSL이라면 RTCUR을 단위 필드로 설정
    WHEN 'HSL'.
      gs_fcat-cfieldname = 'RTCUR'.
  ENDCASE.

  APPEND gs_fcat TO gt_fcat.
  CLEAR gs_fcat.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_layout
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_layout .

*-- Layout 정보 세팅
  gs_layout-zebra      = abap_true.   " 얼룩이 세팅
  gs_layout-cwidth_opt = 'A'.         " 필드 넓이 자동 지정
  gs_layout-sel_mode   = 'D'.         " 여러 줄 선택가능
  gs_layout-stylefname = 'CELL_TAB'.  " cell_tab 등록

  gs_variant-report = sy-repid.
  gs_variant-handle = 'ALV1'.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form create_object
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM create_object .

*-- Docking Container 생성
  CREATE OBJECT go_container
    EXPORTING
      side      = go_container->dock_at_left
      extension = 5000.

*-- ALV_GRID 생성
  CREATE OBJECT go_alv_grid
    EXPORTING
      i_parent = go_container.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_exclude_toolbar
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_exclude_toolbar .

*-- 툴바 제거
  DATA : ls_ui_functions TYPE ui_func.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_undo.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_copy.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_copy_row.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_cut.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_delete_row.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_insert_row.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_append_row.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_paste.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_paste_new_row.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_refresh.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_auf.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_average.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_print.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_graph.
  APPEND ls_ui_functions TO gt_ui_functions.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_screen
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_screen .

  DATA : ls_style TYPE lvc_s_styl,
         lv_tabix TYPE sy-tabix.

  CLEAR : ls_style.
  LOOP AT gt_body INTO gs_body.

    lv_tabix = sy-tabix.

    CLEAR gs_body-cell_tab.

*-- 필드 수정 가능 세팅 -> ' '값으로 전체 필드 오픈
    PERFORM set_style_field USING ' '
                                  cl_gui_alv_grid=>mc_style_enabled
                            CHANGING ls_style.

*-- 필드 수정 불가 세팅 -> pk 값인 'RYEAR' 클로즈
    PERFORM set_style_field USING 'RYEAR'
                                  cl_gui_alv_grid=>mc_style_disabled
                            CHANGING ls_style.

*-- 필드 수정 불가 세팅 -> pk 값인 'RBUKRS' 클로즈
    PERFORM set_style_field USING 'RBUKRS'
                                  cl_gui_alv_grid=>mc_style_disabled
                            CHANGING ls_style.

*-- 필드 수정 불가 세팅 -> pk 값인 'BELNR' 클로즈
    PERFORM set_style_field USING 'BELNR'
                                  cl_gui_alv_grid=>mc_style_disabled
                            CHANGING ls_style.

*-- 변경 사항 반영
    MODIFY gt_body FROM gs_body INDEX lv_tabix TRANSPORTING cell_tab.

  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_style_field
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> P_
*&      --> CL_GUI_ALV_GRID=>MC_STYLE_ENAB
*&      <-- LS_STYLE
*&---------------------------------------------------------------------*
*-- 스타일 세팅 Subroutine.
*-- field mode를 받아 field 별 mode 세팅 및 gs_body에 등록
FORM set_style_field USING pv_field pv_mode
                     CHANGING ps_style TYPE lvc_s_styl.

*-- Style 필드 지정
  ps_style-fieldname = pv_field.
*-- 수정 여부 세팅
  ps_style-style     = pv_mode.
*-- 현재 행에 지정
  INSERT ps_style INTO TABLE gs_body-cell_tab.
  CLEAR ps_style.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_modify_value
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM handle_modify_value USING pv_modified
                               pt_good_cells TYPE lvc_t_modi.

  CHECK pv_modified IS NOT INITIAL. " 수정 데이터가 없다면 EXIT

  LOOP AT pt_good_cells INTO DATA(ls_cells).  " 수정 데이터를 LOOP를 통해 반영

    CLEAR gs_body.
    READ TABLE gt_body INTO gs_body INDEX ls_cells-row_id.  " 수정 데이터 추출

    CASE ls_cells-fieldname.  " 수정 데이터 필드에 따라 수정 사항 반영
*--   RTCUR 필드가 변경됐을 때 단위가 KRW일 때 100이 곱해지는 현상 해결
      WHEN 'RTCUR'.
        CALL FUNCTION 'BAPI_CURRENCY_CONV_TO_INTERNAL'
          EXPORTING
            currency             = gs_body-rtcur " 통화키 세팅
            amount_external      = gs_body-hsl   " 금액
            max_number_of_digits = 15
          IMPORTING
            amount_internal      = gs_body-hsl.  " 변경 금액
    ENDCASE.

*-- 수정 표시
    gs_body-modi_yn = abap_true.

*-- 수정한 데이터 ITAB에 반영
    MODIFY gt_body FROM gs_body INDEX ls_cells-row_id
                                TRANSPORTING modi_yn txt50 hsl.

    PERFORM refresh_screen. " 수정 후 스크린 초기화
  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_edit_toolbar
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> E_INTERACTIVE
*&      --> E_MODIFIED
*&---------------------------------------------------------------------*
FORM handle_edit_toolbar USING pv_interactive
                               po_object TYPE REF TO
                                          cl_alv_event_toolbar_set.

  DATA : lv_input_enable.                   " 토글에 따른 row 생성 삭제 버튼 활성화 여부 판단 변수

  IF gv_mode = 0.                           " 토글이 닫혀있으면 비활성화
    lv_input_enable = abap_true.
  ENDIF.

  CLEAR gs_button.
  PERFORM make_divide_line USING po_object. " 구분선 생성

  PERFORM make_button USING 'TOGL' icon_toggle_display_change " 토글 버튼 생성
                            ' '    po_object.

  PERFORM make_button USING 'IROW' icon_insert_row            " 행추가 버튼 생성
                            lv_input_enable po_object.

  PERFORM make_button USING 'DROW' icon_delete_row            " 행삭제 버튼 생성
                            lv_input_enable po_object.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form make_divide_line
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> PO_OBJECT
*&---------------------------------------------------------------------*
*-- 구분선 생성
*-- butn_type을 '3'으로 설정하여 구분선 생성
FORM make_divide_line USING po_object TYPE REF TO
                                        cl_alv_event_toolbar_set.

  gs_button-butn_type = '3'. " type 3 -> 구분선
  APPEND gs_button TO po_object->mt_toolbar.
  CLEAR gs_button.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form make_button
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> P_
*&      --> ICON_TOGGLE_DISPLAY_CHANGE
*&      --> P_
*&      --> PO_OBJECT
*&---------------------------------------------------------------------*

*-- 버튼 생성 Subroutine function icon mode를 받아 버튼을 생성하고 po_object에 등록
FORM make_button USING pv_func pv_icon pv_mode
                       po_object TYPE REF TO cl_alv_event_toolbar_set.

  gs_button-function = pv_func. " Function 지정
  gs_button-icon     = pv_icon. " Icon 지정
  gs_button-disabled = pv_mode. " 사용 가능 여부 지정
  APPEND gs_button TO po_object->mt_toolbar.
  CLEAR gs_button.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_user_command
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> E_UCOMM
*&---------------------------------------------------------------------*

*-- pv_ucomm에 따른 ALV toolbar 클릭 이벤트 컨트롤
FORM handle_user_command USING pv_ucomm.

  CASE pv_ucomm.
    WHEN 'TOGL'.
      PERFORM set_toggle.     " 토글 버튼 세팅
    WHEN 'IROW'.
      PERFORM set_insert_row. " 열 추가 버튼 세팅
    WHEN 'DROW'.
      PERFORM set_delete_row. " 열 삭제 버튼 세팅
  ENDCASE.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_toggle
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_toggle . " 토글 버튼 클릭에 따른 수정 출력 모드 변경

  gv_mode = ( gv_mode + 1 ) MOD 2.             " gv_mode가 0 or 1로 변경됨

  go_alv_grid->set_ready_for_input( gv_mode ). " gv_mode값 기준 수정, 출력 모드 세팅

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_insert_row
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_insert_row .

  DATA : ls_style TYPE lvc_s_styl.  " 스타일 세팅 WA

  CLEAR : gs_body, ls_style. " 새로 넣을 빈 데이터

*-- 새로 생성할 데이터이므로 PK도 열려있어야 함!!!
  PERFORM set_style_field USING ' '
                                cl_gui_alv_grid=>mc_style_enabled
                          CHANGING ls_style.

*-- 근데 txt50은 유저가 수정해서는 안됨
  PERFORM set_style_field USING 'TXT50'
                                cl_gui_alv_grid=>mc_style_disabled
                          CHANGING ls_style.

  APPEND gs_body TO gt_body.

  PERFORM refresh_screen.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form refresh_screen
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM refresh_screen . " ALV 갱신

  DATA : ls_stbl TYPE lvc_s_stbl. " 갱신 시에도 마우스 위치 고정

  ls_stbl-col = abap_true.  " col 고정
  ls_stbl-row = abap_true.  " row 고정

  CALL METHOD go_alv_grid->refresh_table_display
    EXPORTING
      is_stable = ls_stbl.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form register_event
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM register_event .

  " 변경 감지 이벤트 등록
  go_alv_grid->register_edit_event( cl_gui_alv_grid=>mc_evt_enter ).
  go_alv_grid->register_edit_event( cl_gui_alv_grid=>mc_evt_modified ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_delete_row
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_delete_row .

  DATA : lt_roid TYPE lvc_t_roid. " 삭제할 행 받는 ITAB

  go_alv_grid->get_selected_rows( IMPORTING et_row_no = lt_roid ).  " 선택한 행 받아오기

  SORT lt_roid BY row_id DESCENDING.  " 역순 정렬을 통한 밑에서부터 지우기
  LOOP AT lt_roid INTO DATA(ls_roid). " lt_roid 순환

    CLEAR : gs_body, gs_body_d.
    READ TABLE gt_body INTO gs_body INDEX ls_roid-row_id. " 삭제할 행 추출
    gs_body_d = CORRESPONDING #( gs_body ).               " 삭제할 행 데이터 gs_body_d(삭제용 WA)로 이동
    APPEND gs_body_d TO gt_body_d.                        " 삭제할 행 gt_body_d에 넣기

    DELETE gt_body INDEX ls_roid-row_id.                  " ITAB에서 삭제

  ENDLOOP.

  PERFORM refresh_screen. " 화면 갱신
ENDFORM.
*&---------------------------------------------------------------------*
*& Form save_changed
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM save_changed .

  DATA : ls_save         TYPE ztc1_docu_16,   " 수정 데이터 Transparent Table 반영 용 WA
         lv_answer,
         lv_delete_check TYPE sy-subrc.       " Delete 잘 됐는지 확인 변수

  go_alv_grid->check_changed_data( ).         " 변경 사항 체크 -> 수정하고 탭이나 엔터 안눌러도 반영
*--------------------------------------------------------------------*
* 변경 사항이 있는지 체크
*--------------------------------------------------------------------*
*-- (modi_yn = abap_true 하나라도 있으면 변경사항 있음)
  CLEAR gs_body.
  READ TABLE gt_body INTO gs_body WITH KEY modi_yn = abap_true.

*-- gt_body_d(삭제 행 저장 ITAB)이 비어있으면 변경사항 없음
  IF ( gs_body IS INITIAL ) AND
     ( gt_body_d IS INITIAL ).
    MESSAGE s002 DISPLAY LIKE 'E'.
    EXIT.
  ENDIF.

*--------------------------------------------------------------------*
* Popup 창으로 최종 확인
**********************************************************************
  PERFORM call_pop_up USING lv_answer.  " 팝업 창으로 lv_answer에 유저 답변 저장
  CHECK lv_answer = '1'.                " 값이 '1'이라면 OK 아니면 NO

*--------------------------------------------------------------------*
* Timestamp 및 변경 테이블 세팅
*--------------------------------------------------------------------*
  PERFORM set_time_save USING ls_save.

*--------------------------------------------------------------------*
* 데이터 삭제 Transparent table에 반영
*--------------------------------------------------------------------*
  DELETE ztc1_docu_16 FROM TABLE gt_body_d.
  lv_delete_check = sy-subrc.

*--------------------------------------------------------------------*
* 데이터 수정 Tranparent table에 반영
*--------------------------------------------------------------------*
  MODIFY ztc1_docu_16 FROM TABLE gt_body_s.

  IF ( sy-subrc = 0 ) AND
     ( lv_delete_check = 0 ).      " 테이블 반영 성공 시
    COMMIT WORK AND WAIT.          " 테이블 수정사항 확정
    PERFORM set_screen.            " 새 데이터 pk 및 txt50 잠그기
    PERFORM refresh_screen.        " 화면 갱신
    CLEAR : gt_body_d, gs_body_d.  " 문제 없으니 사용한 테이블 및 WA 초기화
    CLEAR : gt_body_s.             " 문제 없으니 사용한 테이블 초기화

    MESSAGE s000 WITH 'Save Ok!!'. " 메시지 출력
  ELSE.
    ROLLBACK WORK.                 " 실패하면 끝
    MESSAGE s000 WITH 'Error!!'.   " 기존으로 롤백
  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_time_save
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_time_save USING ps_save TYPE ztc1_docu_16.

  DATA : lv_tabix TYPE sy-tabix.

  LOOP AT gt_body INTO gs_body WHERE modi_yn = abap_true.

    lv_tabix = sy-tabix.

    CLEAR gs_body-modi_yn.                    " 수정한거 반영했으니 modi_yn 초기화

    IF gs_body-erdat IS INITIAL.              " 생성 정보가 비어있다면 새로운 데이터
      gs_body = VALUE #( BASE gs_body         " 생성 정보 세팅
                          erdat = sy-datum
                          ernam = sy-uname
                          erzet = sy-uzeit ).
    ELSE.
      gs_body = VALUE #( BASE gs_body         " 수정 정보 세팅
                          aedat = sy-datum
                          aenam = sy-uname
                          aezet = sy-uzeit ).
    ENDIF.

    CLEAR ps_save.
    ps_save = CORRESPONDING #( gs_body ).    " 수정 데이터 ps_save(테이블 반영 WA)로 이동
    APPEND ps_save TO gt_body_s.             " 수정 데이터 pt_save(테이블 반영 ITAB)로 이동

*-- 변경 사항 Main ITAB에 반영
    MODIFY gt_body FROM gs_body INDEX lv_tabix TRANSPORTING
                                                erdat ernam erzet
                                                aedat aenam aezet
                                                modi_yn.

  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form call_pop_up
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> LV_ANSWER
*&---------------------------------------------------------------------*
FORM call_pop_up USING pv_answer.

*-- Popup 창 호출 및 유저 응답 pv_answer에 저장해 전달
  CALL FUNCTION 'POPUP_TO_CONFIRM'
    EXPORTING
      titlebar              = 'User check'
      text_question         = 'Save OK?'
      text_button_1         = 'Yes'
      icon_button_1         = 'ICON_OKAY'
      text_button_2         = 'No'
      icon_button_2         = 'ICON_CANCEL'
      default_button        = '1'
      display_cancel_button = 'X'
    IMPORTING
      answer                = pv_answer.

ENDFORM.
